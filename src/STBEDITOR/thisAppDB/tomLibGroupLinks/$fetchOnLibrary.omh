If tlDatabases.DAM='WebService'
	;  pass parameters to web service
	Do ioWeb.$callFetch($cinst,lrError,row($cinst.$class().$name,$cmethod().$name,row(pnLibRef),$cinst.$type,0,$cinst.ilRESTextensions))
	Calculate lrWebResponse as ioWeb.irResponse
	Calculate irErrorSQL as lrError
	If lrWebResponse.httpStatusCode=200
		;  the result should be in this object     ;; however there may be a SQL error
		If irErrorSQL.NativeErrorText<>''
			Quit method kFalse
		End If
		Quit method kTrue
	Else
		Quit method kFalse
	End If
End If

Do $cinst.$ImmediateSession(loSession,loStatement) Returns lbImmediateSession
If lbImmediateSession
	Do $cinst.$SetSession(loSession,loStatement)
Else If not(ibGoodConnectionToDb)
	Quit method kFalse
End If

Begin statement
Sta: {select [$cinst.$selectnames()]}
Sta: {from  [$cinst.$servertablenames()]}
Sta: {where OLG_OML_REF = @[pnLibRef]}
End statement

Do $cinst.$executesqlandfetch() Returns lbOK

If lbImmediateSession
	Do $cinst.$SetSession(#NULL,#NULL,kTrue)
End If

Quit method lbOK