If tlDatabases.DAM='WebService'
	;  pass parameters to web service
	;  $callService (pFieldReference, row(TableClassName, CurrentMethodName, p1,...,pn) )     ;; result will be returned in pFieldReference, row() contains the parameters for the call
	Do ioWeb.$callFetch($cinst,irErrorSQL,row($cinst.$class().$name,$cmethod().$name,row(pFrom,pTo,pCode,pVhostNo),$cinst.$type,0,ilRESTextensions))
	If ioWeb.irResponse.httpStatusCode=200
		;  the result should be in this object     ;; however there may be a SQL error
		If irErrorSQL.NativeErrorText<>''
			Quit method kFalse
		End If
		Quit method kTrue
	Else
		Quit method kFalse
	End If
End If

Do $cinst.$ImmediateSession(loSession,loStatement) Returns lbImmediateSession
If lbImmediateSession
	Do $cinst.$SetSession(loSession,loStatement)
Else If not(ibGoodConnectionToDb)
	Quit method kFalse
End If

Calculate pTo as dadd(kDay,1,pTo)     ;; date col to datetime

Begin statement
Sta: {select [$cinst.$selectnames()]}
Sta: {from   [$cinst.$servertablenames()]}
If $cinst.$sessionobject.$damname='OMSQLDAM'
	Sta: {where SLE_CWHEN between '[pFrom]' and '[pTo]'}
Else     ;; $cinst.$sessionobject.$damname='PGSQLDAM'
	Sta: {where SLE_CWHEN between @[pFrom] and @[pTo]}
End If
If pCode<>''
	Sta: {and     SLE_CODE = @[pCode]}
End If
If pVhostNo>0
	Sta: {and     SLE_VHOST_REF = @[pVhostNo]}
End If
End statement
Do $cinst.$executesqlandfetch() Returns lbOK
If lbImmediateSession
	Do $cinst.$SetSession(#NULL,#NULL,kTrue)
End If

Quit method lbOK