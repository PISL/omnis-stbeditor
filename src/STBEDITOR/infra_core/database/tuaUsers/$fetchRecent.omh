If tlDatabases.DAM='WebService'
	;  pass parameters to web service
	;  $callService (pFieldReference, row(TableClassName, CurrentMethodName, p1,...,pn) )     ;; result will be returned in pFieldReference, row() contains the parameters for the call
	Do ioWeb.$callFetch($cinst,irErrorSQL,row($cinst.$class().$name,$cmethod().$name,row(pdFrom,pdTo),$cinst.$type,0,ilRESTextensions))
	If ioWeb.irResponse.httpStatusCode=200
		;  the result should be in this object     ;; however there may be a SQL error
		If irErrorSQL.NativeErrorText<>''
			Quit method kFalse
		End If
		Quit method kTrue
	Else
		Quit method kFalse
	End If
End If

Do $cinst.$ImmediateSession(loSession,loStatement) Returns lbImmediateSession
If lbImmediateSession
	Do $cinst.$SetSession(loSession,loStatement)
Else If not(ibGoodConnectionToDb)
	Quit method kFalse
End If

Calculate pdTo as dadd(kDay,1,pdTo)     ;; so Short date picks up rows from datetime column

Begin statement
Sta: {select [$cinst.$selectnames()]}
Sta: {from [$cinst.$servertablenames]}
Sta: {where  exists (}
Sta: {select 'x' from [lrLogOfLogins.$servertablenames()]}
If pdTo>dat('1 JAN 2010')
	Sta: {where ULA_LOGIN between @[pdFrom] and @[pdTo]}
Else
	Sta: {where ULA_LOGIN >= @[pdFrom]}
End If
Sta: {and    ULA_USR_REF = USR_SEQ}
Sta: {)}
End statement

Do $cinst.$executesqlandfetch() Returns lbOK
If lbImmediateSession
	Do $cinst.$SetSession(#NULL,#NULL,kTrue)
End If

Quit method lbOK