If tlDatabases.DAM='WebService'
	;  pass parameters to web service
	;  $callService (pFieldReference, row(TableClassName, CurrentMethodName, p1,...,pn) )     ;; result will be returned in pFieldReference, row() contains the parameters for the call
	Do ioWeb.$callFetch($cinst,irErrorSQL,row($cinst.$class().$name,$cmethod().$name,row(pcText,pClass,pIncludeNonActive,pAppend),$cinst.$type,0,ilRESTextensions))
	If ioWeb.irResponse.httpStatusCode=200
		;  the result should be in this object     ;; however there may be a SQL error
		If irErrorSQL.NativeErrorText<>''
			Quit method kFalse
		End If
		Quit method kTrue
	Else
		Quit method kFalse
	End If
End If

Calculate pcText as con('%',pcText,'%')

Do $cinst.$ImmediateSession(loSession,loStatement) Returns lbImmediateSession
If lbImmediateSession
	Do $cinst.$SetSession(loSession,loStatement)
Else If not(ibGoodConnectionToDb)
	Quit method kFalse
End If

Begin statement
Sta: {select [$cinst.$selectnames()]}
Sta: {from  [$cinst.$servertablenames()]}
Sta: {where ( [icPrefix]CLASS ilike @[pcText]}
Sta: {or      [icPrefix]VALUE ilike @[pcText]}
Sta: {or      [icPrefix]DESC ilike @[pcText]}
Sta: {or      [icPrefix]CHAR ilike @[pcText]}

Sta: {or       cast( [icPrefix]INT as text) like @[pcText]}
Sta: {or       cast( [icPrefix]NUMBER as text) like @[pcText]}
Sta: {)}
If pClass<>''
	Sta: {and     [icPrefix]CLASS = @[pClass]}
End If
If pIncludeNonActive
	;  exclude the active items clause
Else
	Sta: {and [icPrefix]ACTIVE = 1}
End If
End statement
Do $cinst.$executesqlandfetch($cinst,pAppend)

If lbImmediateSession
	Do $cinst.$SetSession(#NULL,#NULL,kTrue)
End If