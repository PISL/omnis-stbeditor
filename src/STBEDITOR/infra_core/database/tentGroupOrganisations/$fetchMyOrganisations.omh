If tlDatabases.DAM='WebService'
	;  pass parameters to web service
	;  $callService (pFieldReference, row(TableClassName, CurrentMethodName, p1,...,pn) )     ;; result will be returned in pFieldReference, row() contains the parameters for the call
	Do ioWeb.$callFetch($cinst,irErrorSQL,row($cinst.$class().$name,$cmethod().$name,row(),$cinst.$type,0,ilRESTextensions))
	If ioWeb.irResponse.httpStatusCode=200
		;  the result should be in this object     ;; however there may be a SQL error
		If irErrorSQL.NativeErrorText<>''
			Quit method kFalse
		End If
		Quit method kTrue
	Else
		Quit method kFalse
	End If
End If

If $ctask.trCurrUser.USR_SEQ>0
	;  proceed
Else
	Quit method kFalse
End If

Do $cinst.$ImmediateSession(loSession,loStatement) Returns lbImmediateSession
If lbImmediateSession
	Do $cinst.$SetSession(loSession,loStatement)
Else If not(ibGoodConnectionToDb)
	Quit method kFalse
End If


Do $cinst.$Sql(,,kFalse,kTrue) Returns lcSQL

Begin statement
Sta: {select x.* from (}     ;; we do not want the UGO columns; that join is just to filter the results
Sta: {[lcSQL]}
Sta: {) x}
Sta: {join  [lrLinkToGroupOrg.$servertablenames] on  UGO_GO_REF = GO_SEQ}
Sta: {where  UGO_USR_REF = @[$ctask.trCurrUser.USR_SEQ]}
End statement


Do $cinst.$executesqlandfetch() Returns lbOK
If lbImmediateSession
	Do $cinst.$SetSession(#NULL,#NULL,kTrue)
End If

Quit method lbOK