
Do method InitDebugVars
If pForeignCol<>''&(pKey<>''|pKey<>0)
	;  continue
Else
	Quit method
End If

If tlDatabases.DAM='WebService'
	;  pass parameters to web service
	;  $callService (pFieldReference, row(TableClassName, CurrentMethodName, p1,...,pn) )     ;; result will be returned in pFieldReference, row() contains the parameters for the call
	Calculate ldtStart as #D
	Do ioWeb.$callFetch($cinst,irErrorSQL,row($cinst.$class().$name,$cmethod().$name,row(pKey,pForeignCol,pOtherCriteria),$cinst.$type,0,ilRESTextensions))
	Calculate inServiceMS as ddiff(kCentiSecond,ldtStart,#D)*10
	Calculate inRESTserverMS as ioWeb.inServerTime
	If ioWeb.irResponse.httpStatusCode=200
		;  the result should be in this object     ;; however there may be a SQL error
		If irErrorSQL.NativeErrorText<>''
			Quit method kFalse
		End If
		Quit method kTrue
	Else
		Quit method kFalse
	End If
End If

Do $cinst.$ImmediateSession(loSession,loStatement) Returns lbImmediateSession
If lbImmediateSession
	Do $cinst.$SetSession(loSession,loStatement)
Else If not(ibGoodConnectionToDb)
	Quit method kFalse
End If

Calculate icStatementType as 'SELECT'

Calculate lnDataType as $cinst.$cols.[pForeignCol].$coltype

If lnDataType=kInteger
	Calculate inID as pKey
	Calculate lcCriteria as con("where ",pForeignCol," = @[inID]")
	
Else If lnDataType=kNumber
	Calculate inREAL as pKey
	Calculate lcCriteria as con("where ",pForeignCol," = @[inREAL]")
	
Else If lnDataType=kDate
	Calculate idDateTime as pKey
	If tim(idDateTime)>tim('00:00')
		Calculate idDateNoTime as pKey
		Calculate lcCriteria as con("where ",pForeignCol," = @[idDateNoTime]")
	Else
		Calculate idDateTime as pKey
		Calculate lcCriteria as con("where ",pForeignCol," = @[idDateTime]")
	End If
Else     ;; lnDataType=kCharacter
	Calculate icChar as pKey
	Calculate lcCriteria as con("where ",pForeignCol," = @[icChar]")
	
End If
If pOtherCriteria<>''
	If left(upp(pOtherCriteria),4)='AND '
		;  Sta: [pOtherCriteria]
		;  leave as is
	Else
		;  Sta: and [pOtherCriteria]
		Calculate pOtherCriteria as con(' and ',pOtherCriteria)
	End If
	Calculate lcCriteria as con(lcCriteria,' ',pOtherCriteria)
End If

Do $cinst.$Sql(,lcCriteria,0,0)

Do $cinst.$executesqlandfetch() Returns lbOK
If lbImmediateSession
	Do $cinst.$SetSession(#NULL,#NULL,kTrue)
End If

Quit method lbOK