If tlDatabases.DAM='WebService'
	;  pass parameters to web service
	;  $callService (pFieldReference, row(TableClassName, CurrentMethodName, p1,...,pn) )     ;; result will be returned in pFieldReference, row() contains the parameters for the call
	Calculate ldtStart as #D
	Do ioWeb.$callInsert($cinst,irErrorSQL,row($cinst.$class().$name,$cmethod().$name,row(pDoNotReportTheseErrors),ilRESTextensions))
	Calculate inServiceMS as ddiff(kCentiSecond,ldtStart,#D)*10
	Calculate inRESTserverMS as ioWeb.inServerTime
	Quit method irErrorSQL.ErrorText='No error has been reported'
End If

Calculate inCountInsert as 0

Do $cinst.$ImmediateSession(loSession,loStatement) Returns lbImmediateSession
If lbImmediateSession
	Do $cinst.$SetSession(loSession,loStatement)
Else If not(ibGoodConnectionToDb)
	Quit method kFalse
End If

Calculate icStatementType as 'INSERT'

Begin statement
Sta: {insert into [$cinst.$servertablenames()]}
Sta: {[$cinst.$insertnames()]}
End statement
Do $cinst.$executesql(pDoNotReportTheseErrors) Returns lbOK
If lbImmediateSession
	Do $cinst.$SetSession(#NULL,#NULL,kTrue)
End If

If $cinst.$statementobject.$rowsaffected>0
	Calculate inCountInsert as inCountInsert+$cinst.$statementobject.$rowsaffected
End If

Quit method lbOK