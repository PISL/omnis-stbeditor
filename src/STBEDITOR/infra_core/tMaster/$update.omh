If tlDatabases.DAM='WebService'
	;  pass parameters to web service
	;  $callService (pFieldReference, row(TableClassName, CurrentMethodName, p1,...,pn) )     ;; result will be returned in pFieldReference, row() contains the parameters for the call
	Do irErrorSQL.$clear()
	Calculate ldtStart as #D
	Do ioWeb.$callUpdate($cinst,irErrorSQL,row($cinst.$class().$name,$cmethod().$name,$cinst,row(pDoNotReportTheseErrors),ilRESTextensions)) Returns lnRecordsAffected
	Calculate inServiceMS as ddiff(kCentiSecond,ldtStart,#D)*10
	Calculate inRESTserverMS as ioWeb.inServerTime
	If isclear(lnRecordsAffected)     ;; could be NULL if call fails to reach the server method
		;  irErrorSQL.NativeErrorText should already have content
		Quit method kFalse
	End If
	Quit method kTrue
End If

Do $cinst.$cols.$findname(con(icPrefix,'MCOUNT')) Returns lRefCol_MCOUNT     ;; 2015-08-07 GRS avoiding notation errors
If lRefCol_MCOUNT
	Calculate lnLastCount as $cinst.[icPrefix]MCOUNT
Else
	Do $cinst.$cols.$findname(con(icPrefix,'MWHEN')) Returns lRefCol_MWHEN
	If lRefCol_MWHEN
		Calculate ldtLastModified as $cinst.[icPrefix]MWHEN
	End If
End If

Calculate inCountUpdate as 0
Do $cinst.$UpdateCols()

Do $cinst.$ImmediateSession(loSession,loStatement) Returns lbImmediateSession
If lbImmediateSession
	Do $cinst.$SetSession(loSession,loStatement)
Else If not(ibGoodConnectionToDb)
	Quit method kFalse
End If

Calculate icStatementType as 'UPDATE'

Begin statement
Sta: {update [$cinst.$servertablenames()]}
Sta: {[$cinst.$updatenames()]}
Sta: {[$cinst.$wherenames()]}
If lRefCol_MCOUNT
	If isnull(lnLastCount)
		Sta: {and    [icPrefix]MCOUNT is null}
	Else
		Sta: {and    [icPrefix]MCOUNT = @[lnLastCount]}
	End If
Else
	If lRefCol_MWHEN&ldtLastModified>dat('1 JAN 2000')
		If $cinst.$sessionobject.$damname='OMSQLDAM'
			Sta: {and    [icPrefix]MWHEN = '[ldtLastModified]'}
		Else
			Sta: {and    [icPrefix]MWHEN = @[ldtLastModified]}
		End If
	End If
End If
End statement
Do $cinst.$executesql(pDoNotReportTheseErrors) Returns lbOK

;  Set reference lrefTemp to $cinst.$statementobject
If $cinst.$statementobject.$rowsaffected>0
	Calculate inCountUpdate as inCountUpdate+$cinst.$statementobject.$rowsaffected
Else
	If irErrorSQL.NativeErrorText<>''
		;  leave as is
	Else
		Calculate irErrorSQL.NativeErrorText as con('Record failed to update in table ',$cinst.$servertablenames(),' / ',$cinst.[icPrimarykey],'.',kCr,kCr,'Perhaps someone else modified it between your reading of the record and your attempt to update.')
		Calculate icErrorMessage as irErrorSQL.NativeErrorText
	End If
	Calculate lbOK as kFalse
End If

If lbImmediateSession
	Do $cinst.$SetSession(#NULL,#NULL,kTrue)
End If

If not(lbOK)
	If lRefCol_MCOUNT
		Calculate $cinst.[icPrefix]MCOUNT as lnLastCount
	Else
		Calculate $cinst.[icPrefix]MWHEN as ldtLastModified
	End If
End If

Quit method lbOK