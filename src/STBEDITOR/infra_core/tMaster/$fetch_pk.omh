
If icPrimarykey<>''&(pKey<>''|pKey<>0)
	;  continue
Else If $cinst.[icPrimarykey]>0
	;  continue
	Calculate pKey as $cinst.[icPrimarykey]
Else
	Quit method
End If

If tlDatabases.DAM='WebService'
	;  pass parameters to web service
	;  $callService (pFieldReference, row(TableClassName, CurrentMethodName, p1,...,pn) )     ;; result will be returned in pFieldReference, row() contains the parameters for the call
	Calculate ldtStart as #D
	Do ioWeb.$callFetch($cinst,irErrorSQL,row($cinst.$class().$name,$cmethod().$name,row(pKey),$cinst.$type,0,ilRESTextensions))
	Calculate inServiceMS as ddiff(kCentiSecond,ldtStart,#D)*10
	Calculate inRESTserverMS as ioWeb.inServerTime
	If ioWeb.irResponse.httpStatusCode=200
		;  the result should be in this object     ;; however there may be a SQL error
		If irErrorSQL.NativeErrorText<>''
			Quit method kFalse
		End If
		Quit method kTrue
	Else
		Quit method kFalse
	End If
End If

Do $cinst.$ImmediateSession(loSession,loStatement) Returns lbImmediateSession
If lbImmediateSession
	Do $cinst.$SetSession(loSession,loStatement)
Else If not(ibGoodConnectionToDb)
	Quit method kFalse
End If

Calculate icStatementType as 'SELECT'

Begin statement
Sta: {select [$cinst.$selectnames()]}
Sta: {from [$cinst.$servertablenames]}

Calculate lnDataType as $cinst.$cols.[icPrimarykey].$coltype

If lnDataType=kInteger
	Calculate inID as pKey
	Sta: {where [icPrimarykey] = @[inID]}
	Calculate lcCriteria as con("where ",icPrimarykey,' = @[inID]')
	
Else If lnDataType=kNumber
	Calculate inID as pKey
	Sta: {where [icPrimarykey] = @[inID]}
	Calculate lcCriteria as con("where ",icPrimarykey,' = @[inID]')
	
Else If lnDataType=kDate
	Calculate idDateTime as pKey
	Sta: {where [icPrimarykey] = @[idDateTime]}
	Calculate lcCriteria as con("where ",icPrimarykey,' = @[idDateTime]')
	
Else If lnDataType=kCharacter
	Calculate icChar as pKey
	If $cinst.$sessionobject.$damname='OMSQLDAM'
		Sta: {where [icPrimarykey] = '[icChar]'}
	Else
		Sta: {where [icPrimarykey] = @[icChar]}
	End If
	Calculate lcCriteria as con(icPrimarykey,' = @[icChar]')
	
Else     ;; lnDataType=kCharacter
	Sta: {[$cinst.$wherenames()]}
	Calculate lcCriteria as con(lcCriteria,pick(lcCriteria<>'','',' and '),$cinst.$wherenames())
End If

End statement

If icForeignCols<>''
	Do $cinst.$Sql(,lcCriteria,0,0)
Else
	;  the above Statement will be executed
End If

Do $cinst.$executesqlandfetch() Returns lbOK



If lbImmediateSession
	Do $cinst.$SetSession(#NULL,#NULL,kTrue)
End If

Quit method lbOK